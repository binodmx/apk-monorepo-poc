plugins {
    // https://github.com/researchgate/gradle-release#multi-project-builds
    // https://github.com/researchgate/gradle-release/issues/102
    id 'net.researchgate.release' version '3.0.2'
    id 'jacoco-report-aggregation'
    id 'base'
}

dependencies {
    jacocoAggregation project(':sample-java-project-1')
    jacocoAggregation project(':sample-java-project-2')
}

reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) { 
            testType = TestSuiteType.UNIT_TEST
        }
    }
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'checkstyle'
    // https://discuss.gradle.org/t/how-can-i-specify-dependency-versions-once-and-reuse-them-in-a-multi-module-project/31916
    // apply from: file("../../dependencies.gradle")

    group = project.group
    version = project.version

    repositories {
        mavenCentral()
    }

    tasks.register('printProjectName', Exec) {
        commandLine 'echo', 'alkgjhweoig'
        doLast {
            println "Project Name: $project.name"
        }
    }

    tasks.named("jacocoTestReport") {
        enabled = false
    }

    jar {
        // https://stackoverflow.com/questions/37875932/generate-pom-xml-in-jar-with-gradle
        into("META-INF/maven/$project.group/$project.name") {
            from { generatePomFileForMavenPublication }
            rename ".*", "pom.xml"
        }

//        from {
//            configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
//        }
    }

    // the GAV of the generated POM can be set here
    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                name 'nexus'
                def snapshotsRepoUrl = 'http://localhost:8081/nexus/content/repositories/snapshots/'
                def releasesRepoUrl = 'http://localhost:8081/nexus/content/repositories/releases/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username rootProject.hasProperty("nexus_username") ? nexus_username : System.getenv("nexus_username")
                    password rootProject.hasProperty("nexus_password") ? nexus_password : System.getenv("nexus_password")
                }
                allowInsecureProtocol = true
            }
        }
    }

     afterReleaseBuild.dependsOn publish

    checkstyle {
        configFile = rootProject.file('checkstyle/checkstyle.xml')
    }
    
    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
        }
    }

    check.dependsOn jacocoTestReport
}

// https://github.com/researchgate/gradle-release/issues/158
release {
    failOnCommitNeeded = false
    failOnUpdateNeeded = false
    tagTemplate = '${name}-${version}'
}

task('printName') {
    doLast {
        println "NEXUS: " + System.env.nexus_username
        println "Name: $project.name"
    }
}
